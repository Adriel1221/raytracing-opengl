#version 430

#define MAX_RECURSION_DEPTH 5

#define T_MIN 0.001f
#define REFLECT_BIAS 0.001f
#define SHADOW_BIAS 0.0001f

#define FLT_MIN 1.175494351e-38
#define FLT_MAX 3.402823466e+38
#define PI_F 3.14159265358979f

#define LIGHT_AMBIENT 0
#define LIGHT_POINT 1
#define LIGHT_DIRECT 2

#define SHADOW_ENABLED 1
#define DBG 0

#define MULTI_INTERSECTION 1
#define CUSTOM_REFRACT 1
#define TOTAL_INTERNAL_REFLECTION 1
#define REFRACTIVE_INDEX_AIR 1.00029
#define DO_FRESNEL 1
// #define LIGHT_AMBIENT2 vec3(0.00)
// #define OBJECT_ABSORB  vec3(0.0, 5.0, 5.0) // for beers law
#define AIM_ENABLED 1

struct rt_material {
	vec3 absorb;

	float diffuse;
	float reflect;
	float refract;
	int specular;
	float kd;
	float ks;
};

struct rt_sphere {
	rt_material material;
	vec3 pos;
	vec3 color;
	vec3 absorb;
	

	float radius;
};

struct rt_light {
	vec3 pos;
	vec3 direction;
	vec3 color;

	int type;
	float intensity;
	float radius;
};

struct rt_scene {
	vec4 quat_camera_rotation;
	vec3 camera_pos;
	vec3 bg_color;

	int canvas_width;
	int canvas_height;
	float viewport_width;
	float viewport_height;

	float viewport_dist;
	int reflect_depth;
	int sphere_count;
	int light_count;
};

layout (local_size_x = 16, local_size_y = 16) in;
layout (rgba32f, binding = 0) uniform image2D img_output;

layout( std430, binding=1 ) readonly buffer scene_buf
{
    rt_scene scene;
};

layout( std430, binding=2 ) readonly buffer spheres_buf
{
    rt_sphere spheres[ ];
};

layout( std430, binding=3 ) readonly buffer lights_buf
{
    rt_light lights[ ];
};

void _dbg()
{
	#if DBG
	ivec2 pixel_coords = ivec2 (gl_GlobalInvocationID.xy);
    imageStore (img_output, pixel_coords, vec4(1,0,0,0));
	#endif
}

void _dbg(float value)
{
	#if DBG
	value = clamp(value, 0, 1);
	ivec2 pixel_coords = ivec2 (gl_GlobalInvocationID.xy);
    imageStore (img_output, pixel_coords, vec4(value,value,value,0));
	#endif
}

void _dbg(vec3 value)
{
	#if DBG
	ivec2 pixel_coords = ivec2 (gl_GlobalInvocationID.xy);
    imageStore (img_output, pixel_coords, vec4(clamp(value, vec3(0), vec3(1)),0));
	#endif
}

const int iterations = 5;
const float maxDist = 1000.0;
const vec3 amb = vec3(1.0);
const float eps = 0.001;

vec4 multiplyQuaternion(vec4 q1, vec4 q2) {
	vec4 result;

	result.w = q1.w * q2.w - q1.x * q2.x - q1.y * q2.y - q1.z * q2.z;
	result.x = q1.w * q2.x + q1.x * q2.w + q1.y * q2.z - q1.z * q2.y;
	result.y = q1.w * q2.y + q1.y * q2.w + q1.z * q2.x - q1.x * q2.z;
	result.z = q1.w * q2.z + q1.z * q2.w + q1.x * q2.y - q1.y * q2.x;

	return result;
}

vec3 Rotate(vec4 q, vec3 v)
{
	vec4 qv = vec4(v, 0);

	vec4 mult = multiplyQuaternion(q, qv);
	float scale = 1 / (q.w * q.w + dot(q, q));
	vec4 inverse = - scale * q;
	inverse.w = scale * q.w;
	vec3 result = vec3(multiplyQuaternion(mult, inverse));

	return result;
}

vec3 getRayDir(ivec2 pixel_coords)
{
	int x = int (pixel_coords.x - scene.canvas_width / 2.0);
	int y = int (pixel_coords.y - scene.canvas_height / 2.0);

	vec3 result = vec3(
		x * scene.viewport_width / scene.canvas_width,
	 	y * scene.viewport_height / scene.canvas_height,
	 	scene.viewport_dist);
	return normalize(Rotate(scene.quat_camera_rotation, result));
}

bool intersectSphere(vec3 ro, vec3 rd, vec3 sp, float radius, float tm, out float t)
{
    bool r = false;
	vec3 v = ro - sp;
	float b = dot(v,rd);
	float c = dot(v,v) - radius*radius;
	t = b*b-c;
    if( t > 0.0 )
    {
		float sqrt_ = sqrt(t);
		t = -b - sqrt_;
		#if MULTI_INTERSECTION
		if (t < 0.0) t = - b + sqrt_;
		#endif
		r = (t > 0.0) && (t < tm);
    } 
    return r;
}

float calcInter(vec3 ro, vec3 rd, out vec3 ob, out vec3 col,out rt_material mat)
{
	float tm = maxDist;
	float t;
	for (int i = 0; i < scene.sphere_count; i++) {
		vec3 pos = spheres[i].pos;
		if(intersectSphere(ro,rd, pos, spheres[i].radius,tm,t)) 
		{ 
			ob = pos; 
			col = spheres[i].color; 
			tm = t; 
			mat = spheres[i].material; 
		}	
	}

	return tm;
}

bool inShadow(vec3 ro,vec3 rd,float d)
{
	float t;
	for (int i = 0; i < scene.sphere_count; i++) {
		if(intersectSphere(ro,rd,spheres[i].pos,spheres[i].radius,d,t)) {
			return true;
		} 
	}
	return false;
}

vec3 LightPixel2 (vec3 pt, vec3 rd, vec3 col, float albedo, vec3 n, float specPower, bool doShadow, float kd, float ks)
{
	float dist, distDiv;
	vec3 lcol,l;
	vec3 diffuse = vec3(0);
	vec3 specular = vec3(0);

	vec3 pixelColor =  vec3(0);
	//if(diffuse > 0.0) //If its not a light
	{
		for (int i = 0; i < scene.light_count; i++) {
			lcol = lights[i].color;
			if (lights[i].type == LIGHT_AMBIENT) {
				pixelColor += lights[i].intensity * col * lcol;
				continue;
			}

			if (lights[i].type == LIGHT_POINT) {
				l = lights[i].pos - pt;
				dist = length(l);
				//distDiv = dist;
				distDiv = 1 + dist*dist; // 1 + dist * dist;
			} else {
				l = - lights[i].direction;
				dist = maxDist;
				distDiv = 1;
			}
			l = normalize(l);
			
			// diffuse
			float dp = clamp(dot(n, l), 0.0, 1.0);
			#if SHADOW_ENABLED
			if (doShadow && (dp <= 0 || inShadow(pt, l, dist))) {
				continue;
			}
			#endif
			diffuse += lcol * col * dp * lights[i].intensity * albedo / distDiv;

			// specular highlight
			if (specPower <= 0.0) continue;
			vec3 reflection = reflect(l, n);
			float specDp = clamp(dot(rd, reflection), 0.0, 1.0);
			if (specDp == 0) continue;
			specular += lcol * pow(specDp, specPower) * dp * lights[i].intensity / distDiv;
		}
		pixelColor = diffuse * kd + specular * ks;
	} //else return col;
	return pixelColor;
}

float getFresnel(vec3 n,vec3 rd,float r0)
{
    float ndotv = clamp(dot(n, -rd), 0.0, 1.0);
	return r0 + (1.0 - r0) * pow(1.0 - ndotv, 5.0);
}

float FresnelReflectAmount (float n1, float n2, vec3 normal, vec3 incident, float refl)
{
    #if DO_FRESNEL
        // Schlick aproximation
        float r0 = (n1-n2) / (n1+n2);
        r0 *= r0;
        float cosX = -dot(normal, incident);
        if (n1 > n2)
        {
            float n = n1/n2;
            float sinT2 = n*n*(1.0-cosX*cosX);
            // Total internal reflection
            if (sinT2 > 1.0)
                return 1.0;
            cosX = sqrt(1.0-sinT2);
        }
        float x = 1.0-cosX;
        float ret = r0+(1.0-r0)*x*x*x*x*x;

        // adjust reflect multiplier for object reflectivity
        ret = (refl + (1.0-refl) * ret);
        return ret;
    #else
    	return refl;
    #endif
}

vec3 getReflection(vec3 ro,vec3 rd)
{
	vec3 color = vec3(0);
	vec3 ob,col;
    rt_material mat;
	float tm = calcInter(ro,rd,ob,col,mat);
	if(tm < maxDist)
	{
		vec3 pt = ro + rd*tm;
		vec3 n = normalize(pt - ob);
		bool outside = dot(rd, n) < 0;
		color = LightPixel2(outside ? pt + n * eps : pt - n * eps,rd,col,mat.diffuse,n,mat.specular, true, mat.kd, mat.ks);
	}
	return color;
}

void swap(inout float a, inout float b)
{
	float tmp = a;
	a = b;
	b = tmp;
}

vec3 refract_c(vec3 I, vec3 N, float ior)
{
	float cosi = clamp(dot(I, N), -1, 1);
	float etai = 1, etat = ior;
	vec3 n = N;
	if (cosi < 0) { cosi = -cosi; }
	else { swap(etai, etat); n = -N; }
	float eta = etai / etat;
	float k = 1 - eta * eta * (1 - cosi * cosi);
	return k < 0.0 ? vec3(0.0) : eta * I + (eta * cosi - sqrt(k)) * n;
}


void main()
{
	ivec2 pixel_coords = ivec2 (gl_GlobalInvocationID.xy);
	if (pixel_coords.x >= scene.canvas_width || pixel_coords.y >= scene.canvas_height){
		return;
	}
	#if AIM_ENABLED
	if (pixel_coords == ivec2(scene.canvas_width / 2, scene.canvas_height / 2))
	{
		imageStore(img_output, pixel_coords, vec4(1,1,1,0));
		return;
	}
	#endif

	float reflectMultiplier,refractMultiplier,tm;
	vec3 ob,col;
    rt_material mat;
	vec3 pt,refCol,n,refl;

	vec3 mask = vec3(1.0);
	vec3 color = vec3(0.0);
	vec3 ro = vec3(scene.camera_pos);
	vec3 rd = getRayDir(pixel_coords);
	float absorbDistance = 0.0;
	
	for(int i = 0; i < iterations; i++)
	{
		tm = calcInter(ro,rd,ob,col,mat);
		if(tm < maxDist)
		{
			pt = ro + rd*tm;
			n = normalize(pt - ob.xyz);
			bool outside = dot(rd, n) < 0;

			#if TOTAL_INTERNAL_REFLECTION
			//float reflIdx = mat.y > 0 ? mat.y : REFRACTIVE_INDEX_AIR;
			if (mat.refract > 0) 
				reflectMultiplier = FresnelReflectAmount( outside ? REFRACTIVE_INDEX_AIR : mat.refract,
													  	  outside ? mat.refract : REFRACTIVE_INDEX_AIR,
											 		      rd, n, mat.reflect);
			else reflectMultiplier = getFresnel(n,rd,mat.reflect);
			#else
			reflectMultiplier = getFresnel(n,rd,mat.reflect);
			#endif
			refractMultiplier = 1 - reflectMultiplier;

			if(mat.refract > 0.0) // Refractive
			{
				if (outside)
				{
					//todo: если прозрачный объект будет внутри другого прозр.объекта, такое не прокатит (absorb = 0)
					//сделать многоуровневый absorbDistance
					absorbDistance = 0.0;
					refl = reflect(rd, n);
					refCol = getReflection(outside ? pt + n * eps : pt - n * eps, refl);
					color += refCol * reflectMultiplier * mask;
					mask *= refractMultiplier;
				}
				else {
					absorbDistance += tm;    
        			vec3 absorb = exp(-mat.absorb * absorbDistance);
					mask *= absorb;
				}
				#if TOTAL_INTERNAL_REFLECTION
				//todo: не делать break, вместо этого сделать rd = reflect(..)
				if (reflectMultiplier >= 1)
				 	break;
				#endif
				ro = outside ? pt - n * eps : pt + n * eps;
				#if CUSTOM_REFRACT
				rd = refract_c(rd, n, mat.refract);
				#else
				float refractCoeff = outside ? REFRACTIVE_INDEX_AIR / mat.y : mat.y / REFRACTIVE_INDEX_AIR;
				rd = refract(rd, outside ? n : -n, refractCoeff);
				#endif
			}
			else 
			if(mat.reflect > 0.0) // Reflective
			{
				ro = outside ? pt + n * eps : pt - n * eps;
				color += LightPixel2(ro, rd, col, mat.diffuse, outside ? n : -n, mat.specular, true, mat.kd, mat.ks)* refractMultiplier * mask;
				rd = reflect(rd, n);
				mask *= reflectMultiplier;
			}
			else // Diffuse
            {
				color += LightPixel2(outside ? pt + n * eps : pt - n * eps, rd, col, mat.diffuse, outside ? n : -n, mat.specular, true, mat.kd, mat.ks) * mask;
                break;
            }
			
		} 
		else {
			color += scene.bg_color * mask;
			break;
		}
	}
	#if DBG == 0
	imageStore (img_output, pixel_coords, vec4(color,1));
	#endif
}